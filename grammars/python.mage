# Should be part of standard library

any_byte = [\x00-\xFF]

any_char = [\x00-\x7F]

eof = !any_byte

extern token indent
extern token dedent

# Helper utilities

@skip
__ = ([\n\r\t ] | '\\' eolf)*

_ = [\t ]*

eol = '\r\n' | '\n'

eolf = eol | eof

line_comment
  = '#' (!eolf any_char)*

eos
  = __ ';'
  | _ line_comment? eol
  | __ eof

# First tokens

@keyword
pub token ident
  = [a-zA-Z_] [a-zA-Z_0-9]*

pub token integer -> Integer
   = [1-9] [0-9]*
   | '0'+

pub token float -> Float
   = [0-9]* '.' [0-9]+

char = any_char

pub token string
   = '"' char* '"'
   | '\'' char* '\''

# General syntax

pub slice
   = lower:expr ':' upper:expr

# Patterns

pub pattern
  = named_pattern
  | attr_pattern
  | subscript_pattern
  | starred_pattern
  | list_pattern
  | tuple_pattern

pub named_pattern
  = name:ident

pub attr_pattern
  = pattern '.' name:ident

pub subscript_pattern
  = pattern '[' slices:((pattern | slice) % ',') ']'

pub starred_pattern
  = '*' expr

pub list_pattern
  = '[' elements:(pattern % ',') ']'

pub tuple_pattern
  = '(' elements:(pattern % ',') ')'

# Expressions

pub expr
  = attr_expr
  | call_expr
  | const_expr
  | infix_expr
  | list_expr
  | named_expr
  | nest_expr
  | prefix_expr
  | starred_expr
  | subscript_expr
  | tuple_expr

pub const_expr
   = literal:(string | float | integer)

pub nest_expr
  = '(' expr ')'

pub named_expr
  = name:ident

pub attr_expr
  = expr '.' name:ident

pub subscript_expr
  = expr '[' slices:((expr | slice) % ',') ']'

pub starred_expr
  = '*' expr

pub list_expr
  = '[' elements:(expr % ',') ']'

pub tuple_expr
  = '(' elements:(expr % ',') ')'

pub arg
  = pos_arg
  | keyword_arg

pub pos_arg
  = expr

pub keyword_arg
  = name:ident '=' expr

pub call_expr
  = operator:expr '(' args:(arg % ',')  ')'

# TODO should not be a token
pub token prefix_op
  = 'not'
  | '+'
  | '-'
  | '~'

pub prefix_expr
  = prefix_op expr

# TODO should not be a token
pub token infix_op
  = '+'
  | '-'
  | '*'
  | '/'
  | '//'
  | '%'
  | '<<'
  | '>>'
  | '|'
  | '^'
  | '&'
  | '@'
  | 'or'
  | 'and'
  | '=='
  | '!='
  | '<'
  | '<='
  | '>'
  | '>='
  | 'is'
  | 'is' 'not'
  | 'in'
  | 'not' 'in'

pub infix_expr
  = left:expr op:infix_op right:expr

# Statements

pub stmt
  = assign_stmt
  | break_stmt
  | class_def
  | continue_stmt
  | delete_stmt
  | expr_stmt
  | for_stmt
  | func_def
  | if_stmt
  | pass_stmt
  | raise_stmt
  | ret_stmt
  | try_stmt
  | type_alias_stmt
  | while_stmt

pub ret_stmt
  = 'return' expr?

pub expr_stmt
  = expr

# TODO make '= expr' optional 
pub assign_stmt
  = pattern annotation:(':' expr)? '=' expr

pub pass_stmt
  = 'pass'

@noskip
body
  = stmt | \indent body:(stmt \eos)* \dedent

pub if_case
   = 'if' test:expr ':' body

pub elif_case
   = 'elif' test:expr ':' body

pub else_case
   = 'else' ':' body

pub if_stmt
  = first:if_case alternatives:elif_case* last:else_case?

pub delete_stmt
  = 'del' pattern

pub raise_stmt
  = 'raise' expr cause:('form' expr)?

pub for_stmt
  = 'for' pattern 'in' expr ':' body else_clause:('else' ':' body)?

# TODO expr should be test:expr
pub while_stmt
  = 'while' expr ':' body else_clause:('else' ':' body)?

pub break_stmt
  = 'break'

pub continue_stmt
  = 'continue'

pub type_alias_stmt
  = 'type' name:ident type_params:('[' (expr % ',') ']')? '=' expr

pub except_handler
  = 'except' expr binder:('as' ident)? body

pub try_stmt
  = 'try' ':' body handlers:except_handler* else_clause:('else' ':' body)? finally_clause:('finally' ':' body)?

# TODO add keyworded bases
pub class_def
  = 'class' name:ident bases:('(' (ident % ',') ')')? ':' body

pub param
  = named_param
  | rest_pos_param
  | rest_keyword_param
  | sep_param

pub named_param
  = pattern annotation:(':' expr)? default:('=' expr)?

pub rest_pos_param
  = '*' name:ident

pub rest_keyword_param
  = '**' name:ident

pub sep_param
  = '*'

pub func_def
  = 'async'? 'def' name:ident '(' params:(param % ',') ')' return_type:('->' expr)? ':' body

# Toplevel expressions

pub module
  = stmts:(stmt \eolf)*

